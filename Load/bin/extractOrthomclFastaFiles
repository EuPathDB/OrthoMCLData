#!/usr/bin/perl

use strict;
use lib "$ENV{GUS_HOME}/lib/perl";
use CBIL::Util::PropertySet;
use DBI;

usage() unless scalar(@ARGV) == 1;
my $targetDir = $ARGV[0];

die "Error: output_dir '$targetDir' does not exist" unless -d $targetDir;

my $dbh = getDbHandle();

# do query to find (abbrev, genus_name, genus_taxon_id)
my $sql = "
with taxonGenus as (select ot.three_letter_abbrev as tla,
                           (select taxon_id
                            from sres.Taxon
                            where rank = 'genus'
                              and taxon_id in (select taxon_id
                                       from sres.Taxon
                                       start with taxon_id = ot.taxon_id
                                       connect by taxon_id = prior parent_id and prior rank != 'genus')) genus_taxon_id
                    from apidb.OrthomclTaxon ot
                    where ot.is_species = 1)
select tg.tla, gtax.ncbi_tax_id, tn.name as genus_sci_name
from taxonGenus tg, sres.TaxonName tn, sres.Taxon gtax
where tg.genus_taxon_id = tn.taxon_id
  and tn.name_class = 'scientific name'
  and tg.genus_taxon_id = gtax.taxon_id
order by genus_sci_name, tla
";
my $stmt = $dbh->prepare($sql);

$stmt->execute();
my $prevGenus;
while (my($organismAbbrev, $genusTaxonId, $genusName) = $stmt->fetchrow_array()) {
  print STDERR "processing $genusName - $organismAbbrev\n";
  $genusName =~ s/ /_/g;  # remove white space
  my $genusDir = "$targetDir/$genusName";
  mkdir($genusDir) unless  -d $genusDir;
  my $idsql = "select ot.three_letter_abbrev || '|' || x.source_id, '', sequence
 from dots.externalaasequence x, apidb.orthomcltaxon ot
 where ot.three_letter_abbrev = '$organismAbbrev'
 and x.taxon_id = ot.taxon_id";
  my @cmd = ('gusExtractSequences', '--outputFile', "$genusDir/$organismAbbrev.fasta", '--idSQL', $idsql);
  system(@cmd) && die "Failed running '" . join(" ", @cmd) . "'\n $?";
}

$dbh->disconnect();

sub usage {
  print STDERR "
Extract fasta files for use in the Tiered Orthomcl Workflow.  Make a directory for each clade (genus).  In each
one place a fasta file named using the orthomcl abbreviation for that organism.  The def lines are in this format:
  >abbrev|id

Usage:  extractOrthomclFastaFiles output_dir

Where:
 - output_dir: is the location to place the clade directories.

";
  exit(1);
}

sub getDbHandle {
  my $gusConfigFile = $ENV{GUS_HOME} . "/config/gus.config";
  my @properties = ();
  my $gusconfig = CBIL::Util::PropertySet->new($gusConfigFile, \@properties, 1);

  my $u = $gusconfig->{props}->{databaseLogin};
  my $pw = $gusconfig->{props}->{databasePassword};
  my $dsn = $gusconfig->{props}->{dbiDsn};

  my $dbh = DBI->connect($dsn, $u, $pw) or die DBI::errstr;
  $dbh->{RaiseError} = 1;
  $dbh->{AutoCommit} = 0;

  return $dbh;
}

