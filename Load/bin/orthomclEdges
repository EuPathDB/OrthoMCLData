#!/usr/bin/perl

use strict;
use DBI;

=pod
SimilarSequences
| InterTaxonMatch
| | BestQueryTaxonScore
| | | BestInterTaxonScore
| | | | BetterHit
| | | | | InParalog ***
| | | | | | InParalog2way--------|
| | | BestHit                    |
| | | | Ortholog ***             |
| | | | | Ortholog2way           |
| | | | | | CoOrthologCandidate -|
| | | | | | | CoOrtholog ***
=cut

my $configFile = $ARGV[0];
my $clean = $ARGV[1];
my $restart = $ARGV[2];

my $stepCount = 1;
my @steps = (['updateMinimumEvalueExponent'],
	     ['bestQueryTaxonScore'],
	     ['qtscore_ix'],
	     ['bestInterTaxonScore'],
	     ['betterHit', ['drop table BestInterTaxonScore']],
	     ['inParalog', ['drop table BetterHit']],
	     ['bestHit', ['drop table BestQueryTaxonScore']],
	     ['ortholog', ['drop table BestHit']],
	     ['orthologAvg'],
	     ['orthologAvgIndex'],
	     ['orthologsNormalization', ['drop table OrthologAvgScore', 'drop table OrthologTemp']],
	     ['inparalogAvg'],
	     ['inparalogAvgIndex'],
	     ['inparalogsNormalization', ['drop table InparalogAvgScore', 'drop table InparalogTemp']],
	     ['ortholog2way'],
	     ['inParalog2way'],
	     ['in2a_ix'],
	     ['in2b_ix'],
	     ['coorthologCandidate', ['drop table ortholog2way', 'drop table inparalog2way']],
	     ['coortholog', ['drop table CoOrthologCandidate']],
	     ['coorthologAvg'],
	     ['coorthologAvgIndex'],
	     ['coorthologsNormalization', ['drop table CoOrthologAvgScore', 'drop table CoOrthologTemp']],
	     ['cleanall', ['truncate table inparalog', 'truncate table ortholog', 'truncate table coortholog']],
	    );

my $stepsHash;
my $cleanHash;
for (my $i=0; $i<scalar(@steps); $i++) {
    $stepsHash->{$steps[$i]->[0]} = $i+1;
    $cleanHash->{$steps[$i]->[0]} = $steps[$i]->[1] if $steps[$i]->[1];
}

print STDERR "\n\n============================================================================================\n";
print STDERR localtime(). " orthomclEdges " . join(' ', @ARGV) . "\n";
print STDERR "=============================================================================================\n\n";

&usage() unless $configFile;
usage() unless $clean =~ /cleanup=(yes|no|only|all)/;
$clean = $1;

my $skipPast;
if ($restart) {
  usage() unless $restart =~ /startAfter=(.*)/;
  $skipPast = $stepsHash->{$1};
  die "invalid restart arg $restart" unless $skipPast;
}

my $config = parseConfigFile($configFile);
my $sst = getConfig("similarSequencesTable");

if (getConfig("dbVendor") == 'oracle') {
  require DBD::Oracle;
} else {
  require DBD::mysql;
}

my $dbh = DBI->connect(getConfig("dbConnectString"), getConfig("dbLogin"),
		       getConfig("dbPassword")) or die DBI::errstr;


commonTempTables();

inparalogs();

orthologs();

normalizeInparalogs();

coorthologs();

cleanall() if $clean eq 'all';

############################################################################

sub parseConfigFile {
  my ($configFile) = @_;

  open(F, $configFile) || die "Can't open config file '$configFile'\n";

  my $config;
  while(<F>) {
    chomp;
    next if /^\#/;
    /^(\w+)\=(.+)/ || die "illegal line in config file '$_'\n";
    my $key=$1;
    my $val=$2;
    $config->{$key} = $val;
    $val = '********' if $key eq 'dbPassword';
    print STDERR localtime() . " configuration: $key=$val\n";
  }
  return $config;
}

sub commonTempTables {
  print STDERR localtime() . " Constructing common temp tables\n"
    unless $clean eq 'only' || $clean eq 'all';
  validateInputTables();
  updateMinimumEvalueExponent();
  bestQueryTaxonScore();
}

sub inparalogs {
  print STDERR localtime() . " Constructing inparalog tables\n"
    unless $clean eq 'only' || $clean eq 'all';
  bestInterTaxonScore();
  betterHit();
  inParalog();
}

sub orthologs {
  print STDERR localtime() . " Constructing ortholog tables\n"
    unless $clean eq 'only' || $clean eq 'all';
  bestHit();
  ortholog();
  normalizeOrthologs();
}

sub coorthologs {
  print STDERR localtime() . " Constructing coortholog tables\n"
    unless $clean eq 'only' || $clean eq 'all';
  inParalog2way();
  ortholog2way();
  coorthologCandidate();
  coortholog();
  normalizeCoOrthologs();
}

sub validateInputTables {

}

sub updateMinimumEvalueExponent {
    my $sql = "select min(evalue_exp)
  from $sst
  where evalue_mant != 0
";

    # a little bit of a hack here.  mysql can't tolerate finding the
    # minEvalueExp in the sql that updates the table
    # so, we do it as a preprocess.
    # must explicitly avoid the preprocess if just cleaning or if skipping
    my $minEvalueExp;
    if ($clean ne 'only' && $clean ne 'all' && !$skipPast) {
      my $stmt = $dbh->prepare("$sql") or die DBI::errstr;
      $stmt->execute() or die DBI::errstr;
      ($minEvalueExp) = $stmt->fetchrow_array();
    }

  $sql = "
update $sst
set evalue_exp = ${minEvalueExp}-1
where evalue_exp = 0
";
  runSql($sql, "updating $sst, setting 0 evalue_exp to underflow value ${minEvalueExp}-1",
	 'updateMinimumEvalueExponent', '23 min');
}

sub bestQueryTaxonScore {
  my $interTaxonMatch = getConfig("interTaxonMatchView");

  my $sql = "
create table BestQueryTaxonScore as
select im.query_id, im.subject_taxon_id, low_exp.evalue_exp, min(im.evalue_mant) as evalue_mant
from $interTaxonMatch im,
     (select query_id, subject_taxon_id, min(evalue_exp) as evalue_exp
      from $interTaxonMatch
      group by query_id, subject_taxon_id) low_exp
where im.query_id = low_exp.query_id
  and im.subject_taxon_id = low_exp.subject_taxon_id
  and im.evalue_exp = low_exp.evalue_exp
group by im.query_id, im.subject_taxon_id, low_exp.evalue_exp
";

  runSql($sql, "create BestQueryTaxonScore", 'bestQueryTaxonScore', '1.5 hours');

  $sql = "
create index qtscore_ix on BestQueryTaxonScore(query_id, subject_taxon_id, evalue_exp, evalue_mant)
";
  runSql($sql, "create qtscore_ix index on BestQueryTaxonScore", 'qtscore_ix', '3 min');
}

sub  bestInterTaxonScore{
  my $sql = "
create table BestInterTaxonScore as
select im.query_id, low_exp.evalue_exp, min(im.evalue_mant) as evalue_mant
from BestQueryTaxonScore im,
     (select query_id, min(evalue_exp) as evalue_exp
      from BestQueryTaxonScore
      group by query_id) low_exp
where im.query_id = low_exp.query_id
  and im.evalue_exp = low_exp.evalue_exp
group by im.query_id, low_exp.evalue_exp
";
  runSql($sql, "create BestInterTaxonScore", 'bestInterTaxonScore', '2 min');
}

sub  betterHit{
  my $evalueExpThreshold = getConfig("evalueExponentCutoff");
  my $percentMatchThreshold = getConfig("percentMatchCutoff");

  my $sql = "
create table BetterHit as
select s.query_id, s.subject_id,
       s.query_taxon_id as taxon_id,
       s.evalue_exp, s.evalue_mant
from $sst s, BestIntertaxonScore bis
where s.query_id != s.subject_id
  and s.query_taxon_id = s.subject_taxon_id
  and s.query_id = bis.query_id
  and s.evalue_exp <= $evalueExpThreshold
  and s.percent_match >= $percentMatchThreshold
  and (s.evalue_mant < 0.001
       or s.evalue_exp < bis.evalue_exp
       or (s.evalue_exp = bis.evalue_exp and s.evalue_mant <= bis.evalue_mant))
-- . . . or Similarity for a protein with no BestIntertaxonScore
--       (i.e. an intrataxon match for a protein with no intertaxon
--        match in the database)
union
select s.query_id, s.subject_id, s.query_taxon_id as taxon_id, s.evalue_exp, s.evalue_mant
from $sst s
where s.query_taxon_id = s.subject_taxon_id
  and s.evalue_exp <= $evalueExpThreshold
  and s.percent_match >= $percentMatchThreshold
  and s.query_id in
     (SELECT distinct sst.query_id
      from $sst sst
      LEFT OUTER JOIN BestIntertaxonScore bis ON bis.query_id = sst.query_id
      WHERE bis.query_id IS NULL)
";
  runSql($sql, "create BetterHit table", 'betterHit', '30 min');
}

sub  inParalog{
  my $sql = "
create table InParalogTemp as
select bh1.query_id as sequence_id_a, bh1.subject_id as sequence_id_b,
       bh1.taxon_id,
       case -- don't try to calculate log(0) -- use rigged exponents of SimSeq
         when bh1.evalue_mant < 0.01 or bh2.evalue_mant < 0.01
           then (bh1.evalue_exp + bh2.evalue_exp) / -2
         else  -- score = ( -log10(evalue1) - log10(evalue2) ) / 2
           (log(10, bh1.evalue_mant * bh2.evalue_mant)
            + bh1.evalue_exp + bh2.evalue_exp) / -2
       end as unnormalized_score
from BetterHit bh1, BetterHit bh2
where bh1.query_id < bh1.subject_id
  and bh1.query_id = bh2.subject_id
  and bh1.subject_id = bh2.query_id
";
  runSql($sql, "create InParalogTemp table", 'inParalog', '5 min');
}

sub bestHit {
  my $evalueExpThreshold = getConfig("evalueExponentCutoff");
  my $percentMatchThreshold = getConfig("percentMatchCutoff");

  my $sql = "
create table BestHit as
select s.query_id, s.subject_id,
       s.query_taxon_id, s.subject_taxon_id,
       s.evalue_exp, s.evalue_mant
from $sst s, BestQueryTaxonScore cutoff
where s.query_id = cutoff.query_id
  and s.subject_taxon_id = cutoff.subject_taxon_id
  and s.query_taxon_id != s.subject_taxon_id
  and s.evalue_exp <= $evalueExpThreshold
  and s.percent_match >= $percentMatchThreshold
  and (s.evalue_mant < 0.01
       or s.evalue_exp = cutoff.evalue_exp
          and s.evalue_mant = cutoff.evalue_mant)
";
  runSql($sql, "create BestHit", 'bestHit', '45 min');
}

sub ortholog {
  my $sql = "
create table OrthologTemp as
select bh1.query_id as sequence_id_a, bh1.subject_id as sequence_id_b,
       bh1.query_taxon_id as taxon_id_a, bh1.subject_taxon_id as taxon_id_b,
       case -- don't try to calculate log(0) -- use rigged exponents of SimSeq
         when bh1.evalue_mant < 0.01 or bh2.evalue_mant < 0.01
           then (bh1.evalue_exp + bh2.evalue_exp) / -2
         else  -- score = ( -log10(evalue1) - log10(evalue2) ) / 2
           (log(10, bh1.evalue_mant * bh2.evalue_mant)
            + bh1.evalue_exp + bh2.evalue_exp) / -2
       end as unnormalized_score
from BestHit bh1, BestHit bh2
where bh1.query_id < bh1.subject_id
  and bh1.query_id = bh2.subject_id
  and bh1.subject_id = bh2.query_id
";
  runSql($sql, "create OrthologTemp table", 'ortholog', '10 min');
}

sub normalizeOrthologs {
  normalizeOrthologsSub("", getConfig("orthologTable"));
}

sub normalizeOrthologsSub {
  my ($co, $orthologTable) = @_;

  my $sql = "
create table ${co}OrthologAvgScore  as
select smaller_tax_id, bigger_tax_id, avg(unnormalized_score) avg_score
from (select case
         when taxon_id_a < taxon_id_b
         then taxon_id_a
         else taxon_id_b
        end as smaller_tax_id,
        case
          when taxon_id_a < taxon_id_b
          then taxon_id_b
          else taxon_id_a
        end as bigger_tax_id,
        unnormalized_score
      from ${co}orthologtemp)
      group by smaller_tax_id, bigger_tax_id
";

  runSql($sql, "create ${co}OrthologAvgScore table", "${co}orthologAvg", '1 min');

  $sql = "create index ${co}orthoAvg_ix on ${co}OrthologAvgScore(smaller_tax_id,bigger_tax_id,avg_score)";

  runSql($sql, "create ${co}OrthologAvgScore index", "${co}orthologAvgIndex", '1 min');

  $sql = "
  insert into $orthologTable (sequence_id_a, sequence_id_b, taxon_id_a, taxon_id_b, unnormalized_score, normalized_score)
  select ot.sequence_id_a, ot.sequence_id_b, ot.taxon_id_a, ot.taxon_id_b, ot.unnormalized_score, ot.unnormalized_score/a.avg_score
  from ${co}OrthologTemp ot, ${co}OrthologAvgScore a
  where (ot.taxon_id_a = a.smaller_tax_id and ot.taxon_id_b = a.bigger_tax_id)
   or (ot.taxon_id_b = a.smaller_tax_id and ot.taxon_id_a = a.bigger_tax_id)
";

  runSql($sql, "populate $orthologTable table, including normalized_score", "${co}orthologsNormalization", '0');
}

sub normalizeInparalogs {
  my $inParalogTable = getConfig("inParalogTable");
  my $orthologTable = getConfig("orthologTable");

  my $sql = "
create table inparalogAvgScore as
     select case
            when haveOrthologsAvg.average is NULL
              then allInparalogsAvg.average
              else haveOrthologsAvg.average
            end as avg_score,
            allInparalogsAvg.taxon_id
     from
       (
         select avg(i2.unnormalized_score) average, i2.taxon_id
         from InParalogTemp i2
         group by i2.taxon_id
       ) allInparalogsAvg LEFT OUTER JOIN
       (
         select avg(i2.unnormalized_score) average, i2.taxon_id
         from InParalogTemp i2
         where (i2.sequence_id_a in
                 (select sequence_id_a from $orthologTable
                  union
                  select sequence_id_b from $orthologTable)
               )
            or (i2.sequence_id_b in 
                 (select sequence_id_a from $orthologTable
                  union 
                  select sequence_id_b from $orthologTable)
               )
         group by i2.taxon_id
       ) haveOrthologsAvg
       ON allInparalogsAvg.taxon_id = haveOrthologsAvg.taxon_id
";
  runSql($sql, "create inParalogAvgScore table", 'inparalogAvg', '1 min');

  $sql = "create index inparalogAvg_ix on inParalogAvgScore(taxon_id,avg_score)";

  runSql($sql, "create inParalogAvgScore index", 'inparalogAvgIndex', '1 min');

  $sql = "
  insert into $inParalogTable (sequence_id_a, sequence_id_b, taxon_id, unnormalized_score, normalized_score)
  select it.sequence_id_a, it.sequence_id_b, it.taxon_id, it.unnormalized_score, it.unnormalized_score/a.avg_score
  from inParalogTemp it, inParalogAvgScore a
  where it.taxon_id = a.taxon_id
";

  runSql($sql, "populate $inParalogTable table, including normalized_score", 'inparalogsNormalization', '0');
}

sub  ortholog2way {
  my $orthologTable = getConfig("orthologTable");

  my $sql = "
create table ortholog2way as
-- symmetric closure of Ortholog
select sequence_id_a, sequence_id_b from $orthologTable
union
select sequence_id_b as sequence_id_a, sequence_id_a as sequence_id_b from $orthologTable
";
  runSql($sql, "create ortholog2way", 'ortholog2way', '1.5 hours');
}

sub  inParalog2way {
  my $inParalogTable = getConfig("inParalogTable");

  my $sql = "
create table inparalog2way as
select sequence_id_a, sequence_id_b from $inParalogTable
union
select sequence_id_b as sequence_id_a, sequence_id_a as sequence_id_b from $inParalogTable
";
  runSql($sql, "create inParalog2way", 'inParalog2way', '1.5 hours');

  $sql = "
create index in2a_ix on inparalog2way(sequence_id_a, sequence_id_b)
";
  runSql($sql, "index in2a_ix", 'in2a_ix', '1.5 hours');

  $sql = "
create index in2b_ix on inparalog2way(sequence_id_b, sequence_id_a)
";
  runSql($sql, "index in2b_ix", 'in2b_ix', '1.5 hours');
}

sub  coorthologCandidate {
  my $sql = "
create table CoorthologCandidate as
select distinct
       least(sequence_id_a, sequence_id_b) as sequence_id_a,
       greatest(sequence_id_a, sequence_id_b) as sequence_id_b
from (-- twp-way union, part 1: inparalog-ortholog-inparalog
      select ip1.sequence_id_a, ip2.sequence_id_b
      from inparalog2way ip1, ortholog2way o, inparalog2way ip2
      where ip1.sequence_id_b = o.sequence_id_a
        and o.sequence_id_b = ip2.sequence_id_a
   union -- part 2: ortholog-inparalog
      select ip.sequence_id_a, o.sequence_id_b
      from inparalog2way ip, ortholog2way o
      where ip.sequence_id_b = o.sequence_id_a)
";
  runSql($sql, "create CoOrthologCandidate", 'coorthologCandidate', '10 min');
}

sub  coortholog {
  my $orthologTable = getConfig("orthologTable");
  my $coOrthologTable = getConfig("coOrthologTable");
  my $evalueExpThreshold = getConfig("evalueExponentCutoff");
  my $percentMatchThreshold = getConfig("percentMatchCutoff");

  my $sql = "
create table CoOrthologTemp as
select /*+ use_nl(candidate, ab) use_nl(candidate, ba)  */
       candidate.sequence_id_a, candidate.sequence_id_b,
       ab.query_taxon_id as taxon_id_a, ab.subject_taxon_id as taxon_id_b,
       case  -- in case of 0 evalue, use rigged exponent
         when ab.evalue_mant < 0.00001 or ba.evalue_mant < 0.00001
           then (ab.evalue_exp + ba.evalue_exp) / -2
         else -- score = ( -log10(evalue1) - log10(evalue2) ) / 2
           (log(10, ab.evalue_mant * ba.evalue_mant)
            + ab.evalue_exp + ba.evalue_exp) / -2
       end as unnormalized_score
from $sst ab, $sst ba, 
     (SELECT sequence_id_a, sequence_id_b
      FROM CoorthologCandidate cc
      LEFT OUTER JOIN $orthologTable o
      ON cc.sequence_id_a = o.sequence_id_a
      AND cc.sequence_id_b = o.sequence_id_b
      WHERE o.sequence_id_a IS NULL) candidate
where ab.query_id = candidate.sequence_id_a
  and ab.subject_id = candidate.sequence_id_b
  and ab.evalue_exp <= $evalueExpThreshold
  and ab.percent_match >= $percentMatchThreshold
  and ba.query_id = candidate.sequence_id_b
  and ba.subject_id = candidate.sequence_id_a
  and ba.evalue_exp <= $evalueExpThreshold
  and ba.percent_match >= $percentMatchThreshold
";
  runSql($sql, "create CoOrthologTemp table", 'coortholog', '30 min');
}

sub normalizeCoOrthologs {
  normalizeOrthologsSub("co", getConfig("coOrthologTable"));
}

sub runSql {
  my ($sql, $msg, $tag, $sampleTime) = @_;

  my $stepNumber = $stepsHash->{$tag};
  die "invalid tag '$tag'" unless $stepNumber;

  if ($skipPast >= $stepNumber) {
    print STDERR "... skipping '$tag'...\n\n";
    return;
  }

  if ($clean ne 'only' && $clean ne 'all') {
      my $t = time();

      print STDERR localtime() . "   $msg  (OrthoMCL-DB V2 took $sampleTime for this step)\n";

      my $stmt = $dbh->prepare($sql) or die DBI::errstr;
      $stmt->execute() or die DBI::errstr;

      my $tt = time() - $t;
      my $hours = int($tt / 3600);
      my $mins = int($tt / 60) % 60;
      if ($hours == 0 && $mins == 0) {$mins = 1};
      my $hoursStr = $hours? "$hours hours and " : "";
      print STDERR localtime() . "   step '$tag' done ($hoursStr$mins mins)\n\n";
  }

  clean($tag) unless ($clean eq 'no');
}

sub clean {
  my ($tag) = @_;

  my $cleanSqls = $cleanHash->{$tag};
  foreach my $cleanSql (@$cleanSqls) {
    if ($cleanSql) {
      $cleanSql =~ /(\w+) table (\w+)/i || die "invalid clean sql '$cleanSql'";
      my $action = $1;
      my $table = uc($2);
      if ($action eq 'drop') {
	my $stmt = $dbh->prepare("select * from all_tables where table_name = '$table'" ) or die DBI::errstr;
	$stmt->execute() or die DBI::errstr;
	return unless $stmt->fetchrow();
      }
      my $stmt = $dbh->prepare($cleanSql) or die DBI::errstr;
      print STDERR localtime() . "   cleaning: $cleanSql\n";
      $stmt->execute() or die DBI::errstr;
      print STDERR localtime() . "   done\n";
    }
  }
}

sub cleanall {
    foreach my $tag (keys (%$cleanHash)) {
	clean($tag);   
    }
}

sub getConfig {
  my ($prop) = @_;
  die "can't find property $prop in config file" unless $config->{$prop};
  return $config->{$prop};
}

sub usage {
  my $stepsString;
  map { $stepsString .= "  $_->[0]\n" } @steps;

  print "
Find edges for OrthoMCL.

usage: orthomclEdges config_file cleanup=[yes|no|only|all] <startAfter=TAG>

where:
  config_file : see below
  cleanup     : clean up temp tables? 
                   yes=clean as we go; 
                   no=don't clean as we go; 
                   only=just clean, do nothing else; 
                   all=just clean, plus clean InParalog, Ortholog and CoOrtholog tables.
  startAfter  : optionally start after a previously completed step. see below for TAGs

Database Input:
  - SimilarSequences table containing all-v-all BLAST hits
  - InParalog, Ortholog, CoOrtholog tables - created but empty

Database Output:
  - Populated InParalog, Ortholog and CoOrtholog tables

Standard Output:
  - matrix file as expected by MCL.

Standard Error:
  - logging info

NOTE: the database login in the config file must have update/insert/truncate privileges on the tables specified in the config file.

Sample Config File:

dbVendor=oracle  (or mysql)
dbConnectString=dbi:Oracle:orthomcl
dbLogin=my_db_login
dbPassword=my_db_password
similarSequencesTable=SimilarSequences
orthologTable=Ortholog
inParalogTable=InParalog
coOrthologTable=CoOrtholog
interTaxonMatchView=InterTaxonMatch
percentMatchCutoff=50
evalueExponentCutoff=-5

Names of TAGs to use in startAfter (look in log file to see last one run)
$stepsString
";
  exit(1);
}

