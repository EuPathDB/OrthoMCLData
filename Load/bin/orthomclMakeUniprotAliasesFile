#!/usr/bin/perl
 
use strict;

usage() unless scalar(@ARGV) == 4;

my ($abbrev, $inputFastaFile, $inputExemplarsFastaFile, $outputAliasesFile) = @ARGV;

open(ALIASES, ">$outputAliasesFile") || die "Can't open '$outputAliasesFile' for writing\n";
open(EXEMPLARS, "$inputExemplarsFastaFile") || die "Can't open '$inputExemplarsFastaFile' for reading\n";
open(FASTA, "$inputFastaFile") || die "Can't open '$inputFastaFile' for reading\n";


# sample defline: >sp|Q8VCI5|PEX19_MOUSE Peroxisomal biogenesis factor 19 OS=Mus musculus GN=Pex19 PE=1 SV=1

# first read in exemplars, to find the protein that is exemplar for each gene

my %exemplars;
while((<EXEMPLARS>)) {
    next unless />\w\w\|(\w+).*GN=([^,\s]+)/; 
    my $geneId = $2;
    my $proteinId = $1;
    $exemplars{$geneId} = $proteinId;
}

my %geneIds;
while(<FASTA>) {
    chomp;
    next unless />\w\w\|(\w+).*GN=(\S+)/; 

    my $proteinId = $1;
    my $genes = $2;
    my @genes = split(/,\s+/, $genes);

    # gene aliases
    my %genes;
    foreach my $gene (@genes) {
	print ALIASES "$abbrev|$genes[0]\t$gene\n" unless $genes{$gene};
	$genes{$gene} = 1;
    }

    # protein alias
    if ($exemplars{$genes[0]} eq $proteinId) {
	print ALIASES "$abbrev|$genes[0]\t$proteinId\tEXEMPLAR\n";  # flag this protein as an exemplar
    } else {
	print ALIASES "$abbrev|$genes[0]\t$proteinId\t\n";  # empty remark column
    }
}

sub usage {

die "
Make a file of repGeneId-geneId and repGeneId-proteinId aliases.  (This includes the unity mapping of repGeneId-repGeneId)  

Usage: orthomclMakeUniprotAliasesFile abbrev input_fasta_file input_exemplars_fasta_file output_aliases_file

Where: 
  abbrev:                       the organism abbreviation for this proteome
  input_fasta_file:             uniprot fasta file
  input_exemplars_fasta_file:   only includes exemplars
  output_aliases_file:          a mapping between the representative gene and gene_ids and transcript_ids of the represented genes.  Three columns:  rep_gene_id, alias_id, remark.  If the alias is the exemplar protein, the remark is EXEMPLAR. Otherwise, it is empty.

";
}
